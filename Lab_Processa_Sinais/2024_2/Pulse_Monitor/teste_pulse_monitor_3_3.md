# Vers√£o 3

[toc]



## Sensor

Nova disposi√ß√£o f√≠sica para o sensor:

![IMG_20241015_154853](IMG_20241015_154853.jpeg)

Em uso:

| Detalhe | Em uso |
| :---: | :---: |
| <img src="IMG_20241016_161959.jpeg" alt="IMG_20241016_161959" style="zoom: 67%;" /> | <img src="IMG_20241016_162209.jpeg" alt="IMG_20241016_162209" style="zoom:67%;" /> |

## Circuito

| Diagrama El√©trico | Diodo Foto-detector | Detalhe led |
| :---: | :---: | :---: |
| ![Foto_acoplador_3](Foto_acoplador_3.png) | ![Foto_acoplador_1](Foto_acoplador_1.png) | ![Foto_acoplador_2](Foto_acoplador_2.png) |

**Obs.:** Note que o foto-diodo √© polarizado inversamente quando usado como sensor.

## C√≥digo

Segue `pulse_monitor_3.ino`:

```c++
/* Monitor de pulsa√ß√£o
   Teste usando foto-detector
   Inclui: 
   1. Dados capturados √† taxa de fs = 50 Hz (T = 0,02 segundos), usando ISR
   2. Filtro passa-faixa [0,5 5] Hz @ fs = 50 Hz
   Fernando Passold, em 15/10/2024
*/

const byte analogPin = A0;     // pino (A/D) que recebe sinal anal√≥gico
const byte monitor_isr = 7;    // pino 7: para monitor freq e dutty-cycle da ISR (oscilosc√≥pio)

bool state = 0;  // vari√°vel ativada dentro da ISR (p/monitorar ISR)

int x;         // dados lidos no A/D 0 do Ardu√≠no
float y;       // sinal x filtrado

int x1, x2, x3, x4;    // valores amostras passadas sinal de entrada x
float y1, y2, y3, y4;  // valores amostras passadas sinal de sa√≠da y (sinal filtrado)
// Par√¢metros do filtro (calculados usando Matlab)
float b0 = 0.056448;  // peso associado com x[n]
float b2 = -0.1129;   // peso associado com x[n-2]; o "b" para x[n-1] √© nulo!
float b4 = 0.056448;  // peso associado com x[n-4]; o "b" para x[n-3] tamb√©m √© nulo
float a0 = 1;         // peso para y[n] ("normalizado")
float a1 = -3.1595;   // peso para y[n-1]
float a2 = 3.7927;    // peso para y[n-2]
float a3 = -2.0826;   // peso para y[n-3]
float a4 = 0.45045;   // peso para y[n-4]

void setup() {
  // put your setup code here, to run once:

  // Programando a ISR √† 50 Hz usando timer 1
  cli();  //stop interrupts
  //set timer1 interrupt at 50 Hz
  TCCR1A = 0;  // set entire TCCR1A register to 0
  TCCR1B = 0;  // same for TCCR1B
  TCNT1 = 0;   //initialize counter value to 0
  // set compare match register for 1hz increments
  OCR1A = 1249;  // = (16*10^6) / (50*256) - 1 (must be <65536)
  // turn on CTC mode
  TCCR1B |= (1 << WGM12);
  // Set CS12 bits for 256 prescaler
  TCCR1B |= (1 << CS12);
  // enable timer compare interrupt
  TIMSK1 |= (1 << OCIE1A);

  pinMode(monitor_isr, OUTPUT);
  digitalWrite(monitor_isr, LOW);

  Serial.begin(115200);  //  setup serial
  while (!Serial) {
    ;  // Aguarda at√© que a porta serial esteja pronta - normalmente 10 ms
  }
  Serial.println(" ");
  Serial.println("Monitorando foto-detector: ");

  // inicializando amostras atrasadas sinal de entrada
  x1 = 0; x2 = 0; x3 = 0; x4 = 0;
  // inicializando amostras atrasas sinal sa√≠da filtro
  y1 = 0; y2 = 0; y3 = 0; y4 = 0;

  sei();  //allow interrupts
}

ISR(TIMER1_COMPA_vect) {  //timer1 interrupt 1Hz toggles pin 13 (LED)
  digitalWrite(monitor_isr, HIGH);
  // Atualiza var√≠avel x contendo valor lido em A0
  x = analogRead(analogPin);  // * (5.0 / 1024.0);  // converte para faixa 0 √† 5 Volts

  // Aplicando fitro Passa-Faixa sobre sinal de entrada x
  // Sa√≠da filtrada em y
  y = b0 * (float)x + b2 * (float)x2 + b4 * (float)x4 - a1 * y1 - a2 * y2 - a3 * y3 - a4 * y4;
  // atualizando amostras passadas para pr√≥xima chamada da ISR
  y4 = y3;
  y3 = y2;
  y2 = y1;
  y1 = y;
  x4 = x3;
  x3 = x2;
  x2 = x1;
  x1 = x;

  // n√£o √© recomend√°vel enviar dados pela serial dentro de um ISR, mas testando...
  Serial.print(x);  // col 1 - valor bruto (int)
  Serial.print(" \t");
  Serial.println(y);  // col 2 - valor filtrado (float)

  digitalWrite(monitor_isr, LOW);
}

void loop() {
  // put your main code here, to run repeatedly:
  // Nothing to do here
}
```


## Resultados

A figura abaixo mostra como fica a captura de dados SEM dedo colocado sobre o par emissor-receptor fotoel√©trico:

![pulse_monitor_3_3_sem_dedo](pulse_monitor_3_3_sem_dedo.gif)

Captura semelhante observando a sa√≠da do filtro:

![pulse_monitor_3_3_sem_dedo_y](pulse_monitor_3_3_sem_dedo_y.gif)

O pr√≥ximo gr√°fico compara $x[n]$ $\times$ $y[n]$ "escalonado":

![pulse_monitor_3_3_sem_dedo_x_y_aux](pulse_monitor_3_3_sem_dedo_x_y_aux.gif)

<!-- Segue um arquivo "bag" com este tipo de dados capturados: -->

A pr√≥xima figura mostra uma sequ√™ncia de captura de dados COM o dedo posicionado de forma algo est√°tica sobre o conjunto emissor-sensor fotoel√©trico:

<!-- ![pulse_monitor_3_3_com_dedo](pulse_monitor_3_3_com_dedo.gif) -->

Gr√°fico dos dados brutos, $x[n]$:

![pulse_monitor_3_3_com_dedo_x](pulse_monitor_3_3_com_dedo_x.gif)

Gr√°fico do valor filtrado, $y[n]$:

![pulse_monitor_3_3_com_dedo_y](pulse_monitor_3_3_com_dedo_y.gif)

A pr√≥xima figura mostra um gr√°fico do tipo $x[n]$ (sinal bruto) $\times$ $y[n]$ (sinal filtrado) "escalonado" ou vari√°vel  `y_aux` calculada da seguinte forma:

```c++
y_aux = 460.0 + 2.0 * y;
```

Esta vari√°vel foi acrescentada depois ao c√≥digo mostrado acima e os valores usados para "escalar" $y[n]$ foram obtidos empiricamente observando ensaios realizados.

√â poss√≠vel se observar simulateamente sinal bruto (value 3) $\times$ sinal filtrado (escalonado) (value 5):

![pulse_monitor_3_3_com_dedo_x_y_aux](pulse_monitor_3_3_com_dedo_x_y_aux.gif)

Note que o filtro consegue "ignorar" adequadamente bem varia√ß√µes abruptas no sinal de entrada $x[n]$ causadas por movimenta√ß√£o do dedo, movimento do dedo sobre sensores, etc:

![pulse_monitor_3_3_com_dedo_x_y_aux2](pulse_monitor_3_3_com_dedo_x_y_aux2.gif)

## Arquivos "bag"

Disponibilizando arquivos de dados capturados para testes offlines de algoritmos de filtros e detec√ß√£o de picos:

* **Sem** dedo posicionado sobre o conjunto sensor/receptor: <a href="captura_sem_dedo.txt" target="_blank">captura\_**sem**\_dedo.txt"</a> (20 segundos; 14 KB)
* **Com** dedo posicionado sobre o conjunto sensor/receptor: <a href="captura_com_dedo.txt" target="_blank">captura\_**com**\_dedo.txt</a> (30 segundos, incluindo nos √∫ltimos segundos, movimenta√ß√£o do dedo sobre sensor; 21 KB).

<!-- Segue um arquivo "bag" com este tipo de dados capturados: [dados_capturados_pulse_monitor_3_3_com_dedo.txt](dados_capturados_pulse_monitor_3_3_com_dedo.txt). -->

Trabalhando com os dados do √∫ltimo arquivo se √© capaz de mostrar $x[n]$ e $y[n]$ ao mesmo tempo, no mesmo gr√°fico:

![pulse_monitor_3_3_com_dedo.png](pulse_monitor_3_3_com_dedo.png)

Voc√™ pode plotar os dados usando o Matlab:

```matlab
>> dados=load('captura_sem_dedo.txt');	% l√™ "bag" file
>> size(dados)		% matriz de 923 x 2, col 1=x[x], col 2=y[n]
ans =
   923     2
>> x=dados(:,1);	% separa x[n] = dados brutos
>> y=dados(:,2);	% separa y[n] = sinal filtrado
>> u=length(x)
u =
   923
>> T=0.02;			% per√≠odo de amostragem adotado (50Hz)
>> t=0:T:(u-1)*T; 	% sintetiza vetor tempo
>> plot(t,x)
```

Seria obtido um gr√°fico como:

![captura_sem_dedo.png](captura_sem_dedo.png)

Note como o sinal se modifica quando o dedo est√° sobre o sensor:

![captura_com_dedo.png](captura_com_dedo.png)

Por isto que se faz necess√°rio passar o sinal bruto $x[n]$ pelo filtro passa-faixa, resultado em:

![captura_com_dedo_y.png](captura_com_dedo_y.png)

**Obs.**: note que os 2 primeiros segundos do sinal filtrado s√£o ignorados, o filtro ainda est√° no seu "regime transit√≥rio".

Os arquivos "bag" podem ser √∫teis para testar diferentes abordagem para **p√≥s-processamento do sinal bruto**, sem necessidade de acessar novamente o hardware para realizar novos ensaios *in-locco*. 

Resta agora detectar os picos e calcular o BPM. 

## P√≥s-processamento do sinal filtrado

Para determinar o BPM, ainda se faz necess√°rio processar o sinal filtrado fazendo:

* **Op√ß√£o 1**: **simples detecc√ß√£o de m√°ximos** consecutivos e determina√ßao da dist√¢ncia (per√≠odo) de tempo entre os mesmos. Note: BMP $=\Delta t \times 60$. Eventualmente se pode aprimorar em algo esta abordagem desonsiderando picos muito pr√≥ximos. E adicionalmente ainda pode ser verificado se existe um "vale de descida" entre os picos, verificando se o sinal caiu abaixo de certo limiar antes de encontrar um novo pico. Abordagem obviamente ==sucept√≠vel √† erros==.
* **Op√ß√£o 2**: **uso de derivada**, ==m√©todo mais robusto==. A abordagem de derivadas no c√≥digo de detec√ß√£o de picos, considera calcular a derivada primeira (ou diferen√ßa entre amostras consecutivas; gradiente do sinal) para identificar quando o sinal est√° subindo ou descendo. Isso pode ajudar a garantir que um pico seja detectado apenas ap√≥s uma subida e uma descida, confirmando a forma√ß√£o de um pico verdadeiro. Essa abordagem reduz a probabilidade de detectar picos falsos que podem ocorrer devido a ru√≠dos ou varia√ß√µes r√°pidas no sinal.




----

<font size="2">üåä [Fernando Passold](https://fpassold.github.io/)[ üì¨ ](mailto:fpassold@gmail.com), <script language="JavaScript"><!-- Hide JavaScript...
var LastUpdated = document.lastModified;
document.writeln ("p√°gina criada em Oct 15, 2024; atualizada em " + LastUpdated); // End Hiding -->
</script></font>